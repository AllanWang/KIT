buildscript {

    repositories {
        mavenCentral()
        jcenter()
    }

    apply plugin: "ca.allanwang.kit"

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kit.kotlin}"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:${kit.spotless}"
    }
}

apply plugin: "ca.allanwang.kit"

allprojects {
    group "ca.allanwang.kit"
    version kit.version
}

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'kotlin'
    apply plugin: 'maven'

    repositories {
        mavenCentral()
        jcenter()
    }

    apply from: '../spotless.gradle'

    test {
        dependsOn cleanTest
        testLogging.showStandardStreams = true
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    dependencies {
        compile kitDependency.kotlin

        testCompile kitDependency.junit
        testCompile kitDependency.kotlinTest
    }

}